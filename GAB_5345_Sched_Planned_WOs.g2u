Program.Sub.ScreenSU.Start
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
	V.Global.sGsGCIcon.Declare
	V.Global.iOpHdrBoolean.Declare
	V.Global.sOpHdrText1.Declare
	V.Global.iOpHdrLong.Declare
	V.Global.sOptResCom2.Declare
	V.Global.sOptResCom.Declare
	
	Program.External.Include.Library("410000.lib")
Program.Sub.Preflight.End

Program.Sub.Main.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare
	
	F.ODBC.Connection!con.OpenCompanyConnection(300)
	
	F.Intrinsic.Control.CallSub(CreateFile)
	
	F.ODBC.Connection!con.Close
	F.Intrinsic.Control.End

	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.Main.End

Program.Sub.CreateFile.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare
	
	V.Local.iCnt.Declare
	V.Local.sData.Declare
	V.Local.sFile.Declare
	V.Local.dDueDate.Declare
	V.Local.sDir.Declare
	V.Local.sDescription.Declare
	
	'Create Directory
	F.Intrinsic.String.Build("{0}\AUTOWO",V.Caller.FilesDir,V.Local.sDir)
	F.Intrinsic.File.CreateDir(V.Local.sDir)
	
	'Set File Path
	F.Intrinsic.String.Build("{0}\UPLIV950.TXT",V.Caller.FilesDir,V.Local.sFile)
	
	'Remove Planned WOs if Source Code is Purchase
	F.ODBC.Connection!con.Execute("DELETE FROM GAB_5345_PLANNED WHERE PART+LOCATION IN (SELECT PART+LOCATION FROM V_INVENTORY_MST2 WHERE GAB_5345_PLANNED.PART = V_INVENTORY_MST2.PART AND GAB_5345_PLANNED.LOCATION = V_INVENTORY_MST2.LOCATION AND CODE_SOURCE NOT IN ('M','F'));")
	
	'Get Orders
	F.Data.DataTable.CreateFromSQL("dtOrders","con","SELECT PART,LOCATION,DESCRIPTION,SCHED_QTY,SCHED_DUE_DATE FROM GAB_5345_PLANNED WHERE SCHED_QTY <> 0 AND SCHED_DUE_DATE IS NOT NULL ORDER BY ORDER_NO, ORDER_LINE, SEQ;",True)
	
	'Save File
	F.Intrinsic.Control.If(V.DataTable.dtOrders.RowCount,=,0)
		F.Intrinsic.UI.Msgbox("There are no Orders to generate WOs for.","No Orders")
	F.Intrinsic.Control.Else
		'Loop through orders and create WO file
		F.Intrinsic.Control.For(V.Local.iCnt,0,V.DataTable.dtOrders.RowCount--,1)
			V.Local.dDueDate.Set(V.DataTable.dtOrders(V.Local.iCnt).SCHED_DUE_DATE!FieldVal)
			F.Intrinsic.String.Replace(V.DataTable.dtOrders(V.Local.iCnt).DESCRIPTION!FieldValTrim,","," ",V.Local.sDescription)
			F.Intrinsic.Control.If(V.Local.iCnt,=,0)
				F.Intrinsic.String.Build("{0},{1},{2},{3},{4}",V.DataTable.dtOrders(V.Local.iCnt).PART!FieldValTrim,V.DataTable.dtOrders(V.Local.iCnt).LOCATION!FieldValTrim,V.Local.sDescription,V.DataTable.dtOrders(V.Local.iCnt).SCHED_QTY!FieldVal,V.Local.dDueDate.FormatYYYYMMDD,V.Local.sData)
			F.Intrinsic.Control.Else
				F.Intrinsic.String.Build("{0}{1}{2},{3},{4},{5},{6}",V.Local.sData,V.Ambient.NewLine,V.DataTable.dtOrders(V.Local.iCnt).PART!FieldValTrim,V.DataTable.dtOrders(V.Local.iCnt).LOCATION!FieldValTrim,V.Local.sDescription,V.DataTable.dtOrders(V.Local.iCnt).SCHED_QTY!FieldVal,V.Local.dDueDate.FormatYYYYMMDD,V.Local.sData)
			F.Intrinsic.Control.EndIf
		F.Intrinsic.Control.Next(V.Local.iCnt)
		'Create file from data
		F.Intrinsic.File.String2File(V.Local.sFile,V.Local.sData)
		'Check for Job Detail
		F.Intrinsic.Control.CallSub(CheckJobDetail)
		'Remove P-WOs
		F.Intrinsic.Control.CallSub(DeletePWOs)
		'Get Options
		F.Intrinsic.Control.CallSub(GetOptions)
		'Set Options for scheduling
		F.Intrinsic.Control.CallSub(SetOptions)
		'Schedule WOs
		F.Intrinsic.Control.CallSub(SchedulePWOs,"File",V.Local.sFile)
		'Assign P-WOs to GAB_5345_PLANNED
		F.Intrinsic.Control.CallSub(AssignPWOs)
		'Reset Options back to their original state
		F.Intrinsic.Control.CallSub(ResetOptions)
	F.Intrinsic.Control.EndIf
	
	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.CreateFile.End

Program.Sub.CheckJobDetail.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare
	
	V.Local.sFile.Declare
	V.Local.sDir.Declare
	
	F.Intrinsic.String.Build("{0}\5345",V.Caller.TempDir,V.Local.sDir)
	F.Intrinsic.File.CreateDir(V.Local.sDir)
	F.Intrinsic.String.Build("{0}\GAB_5345_JobsWithDetail_{1}_{2}.txt",V.Local.sDir,V.Ambient.Date.FormatYYYYMMDD,V.Caller.User,V.Local.sFile)
	
	F.Data.DataTable.CreateFromSQL("dtCheckJobDetail","con","SELECT DISTINCT JOB,SUFFIX,SEQ,DATE_SEQUENCE FROM V_JOB_DETAIL A WHERE JOB LIKE 'P%' AND (SELECT SUM(AMOUNT_LABOR) FROM V_JOB_DETAIL B WHERE A.JOB = B.JOB AND A.SUFFIX = B.SUFFIX) <> 0;",True)
	F.Intrinsic.Control.If(V.DataTable.dtCheckJobDetail.RowCount,<>,0)
		'Stop Process and Export file
		F.Data.DataTable.SaveToCSV("dtCheckJobDetail",V.Local.sFile,True)
'		F.Intrinsic.Task.ShellExec(0,"",V.Local.sFile,"","",1)
		F.ODBC.Connection!con.Close
		F.Intrinsic.Control.End
	F.Intrinsic.Control.EndIf

	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.CheckJobDetail.End

Program.Sub.DeletePWOs.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare
	
	V.Local.iCnt.Declare
	V.Local.sSQL.Declare
	V.Local.sTableNames.Declare
	V.Local.sTableField.Declare
	
	V.Local.sTableNames.Set("JOB_HEADER!*!JOB*!*JOB_OPERATIONS!*!JOB*!*JOB_DETAIL!*!JOB*!*JOB_DTL_SUPP!*!JOB*!*JOB_COMMITMENTS!*!JOB*!*WO_SEQ_COMMENTS!*!WO*!*SCHED_WO_SUPP!*!WO*!*APSV3_JBLINES!*!JOB*!*APSV3_JBMASTER!*!JOB*!*APSV3_UBDETAIL!*!JOB")
	F.Intrinsic.String.Split(V.Local.sTableNames,"*!*",V.Local.sTableNames)
	
	F.Intrinsic.Control.For(V.Local.iCnt,0,V.Local.sTableNames.UBound,1)
		F.Intrinsic.String.Split(V.Local.sTableNames(V.Local.iCnt),"!*!",V.Local.sTableField)
		F.Intrinsic.String.Build("Delete From {0} Where {1} Like 'P%';",V.Local.sTableField(0),V.Local.sTableField(1),V.Local.sSQL)
		F.ODBC.Connection!con.Execute(V.Local.sSQL)
	F.Intrinsic.Control.Next(V.Local.iCnt)

	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.DeletePWOs.End

Program.Sub.GetOptions.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare
	
	'0 = Boolean
	'1 = String
	'2 = Float
	'3 = Long
	'4 = Date/Time
	
	'Pull Options from OP_HEADER
	F.Global.General.ReadOption("401172",0,0,0,V.Global.iOpHdrBoolean)
	F.Global.General.ReadOption("401172",1,"",1,V.Global.sOpHdrText1)
	F.Global.General.ReadOption("401172",3,-1,2,V.Global.iOpHdrLong)
	
	'Pull Options from OPT_RES_COM2
	F.ODBC.Connection!con.ExecuteAndReturn("SELECT RTRIM(AUTO_NO_WO_W_PREFIX) FROM OPT_RES_COM2 WHERE FUNCT = 'RS' AND TYPE = 'CO2';",V.Global.sOptResCom2)
	F.ODBC.Connection!con.ExecuteAndReturn("SELECT RTRIM(COMPANY) FROM OPT_RES_COM WHERE FUNCT = 'RS' AND TYPE = 'WO';",V.Global.sOptResCom)
	
	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.GetOptions.End

Program.Sub.SetOptions.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare

	'Set Options in OP_HEADER
	F.ODBC.Connection!con.Execute("UPDATE OP_HEADER SET F_BOOLEAN = '1' WHERE ID = '401172' AND SEQUENCE = '0000';")
	F.ODBC.Connection!con.Execute("UPDATE OP_HEADER SET TEXT1 = 'P' WHERE ID = '401172' AND SEQUENCE = '0001';")
	F.ODBC.Connection!con.Execute("UPDATE OP_HEADER SET F_LONG = 0 WHERE ID = '401172' AND SEQUENCE = '0002';")
	
	'Set Options in OPT_RES_COM2
	F.ODBC.Connection!con.Execute("UPDATE OPT_RES_COM2 SET AUTO_NO_WO_W_PREFIX = 'Y' WHERE FUNCT = 'RS' AND TYPE = 'CO2';")
	F.ODBC.Connection!con.Execute("UPDATE OPT_RES_COM SET COMPANY = 'P00000' WHERE FUNCT = 'RS' AND TYPE = 'WO';")
	
	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.SetOptions.End

Program.Sub.SchedulePWOs.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare

	F.Global.General.CallWrapperSync(50,"INV950/S")
'	F.Intrinsic.File.DeleteFile(V.Args.File)

	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.SchedulePWOs.End

Program.Sub.AssignPWOs.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare

	F.ODBC.Connection!con.Execute("UPDATE GAB_5345_PLANNED SET A.P_JOB = B.JOB, A.P_SFX = B.SUFFIX FROM GAB_5345_PLANNED A JOIN V_JOB_HEADER B ON A.PART = B.PART AND A.LOCATION = B.LOCATION AND A.SCHED_QTY = B.QTY_ORDER AND A.SCHED_DUE_DATE = B.DATE_DUE WHERE JOB LIKE 'P%';")

	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.AssignPWOs.End

Program.Sub.ResetOptions.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare
	
	V.Local.sSQL.Declare

	'Reset Options in OP_HEADER
	F.Intrinsic.String.Build("UPDATE OP_HEADER SET F_BOOLEAN = '{0}' WHERE ID = '401172' AND SEQUENCE = '0000';",V.Global.iOpHdrBoolean,V.Local.sSQL)
	F.ODBC.Connection!con.Execute(V.Local.sSQL)
	F.Intrinsic.String.Build("UPDATE OP_HEADER SET TEXT1 = '{0}' WHERE ID = '401172' AND SEQUENCE = '0001';",V.Global.sOpHdrText1,V.Local.sSQL)
	F.ODBC.Connection!con.Execute(V.Local.sSQL)
	F.Intrinsic.String.Build("UPDATE OP_HEADER SET F_LONG = {0} WHERE ID = '401172' AND SEQUENCE = '0002';",V.Global.iOpHdrLong,V.Local.sSQL)
	F.ODBC.Connection!con.Execute(V.Local.sSQL)
	
	'Reset Options in OPT_RES_COM2
	F.Intrinsic.String.Build("UPDATE OPT_RES_COM2 SET AUTO_NO_WO_W_PREFIX = '{0}' WHERE FUNCT = 'RS' AND TYPE = 'CO2';",V.Global.sOptResCom2,V.Local.sSQL)
	F.ODBC.Connection!con.Execute(V.Local.sSQL)
	F.Intrinsic.String.Build("UPDATE OPT_RES_COM SET COMPANY = '{0}' WHERE FUNCT = 'RS' AND TYPE = 'WO';",V.Global.sOptResCom,V.Local.sSQL)
	F.ODBC.Connection!con.Execute(V.Local.sSQL)

	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.ResetOptions.End

Program.Sub.Comments.Start

'Options:
'--Automatic Numbering when not Generated from a Sales Order
	'SELECT F_BOOLEAN FROM OP_HEADER WHERE ID = 401172 AND SEQUENCE = '0000';
	'SELECT AUTO_NO_WO_W_PREFIX FROM OPT_RES_COM2 WHERE FUNCT = 'RS' AND TYPE = 'CO2';

'--with Prefix
	'SELECT TEXT1 FROM OP_HEADER WHERE ID = 401172 AND SEQUENCE = '0001';

'--Last Number
	'SELECT F_LONG FROM OP_HEADER WHERE ID = 401172 AND SEQUENCE = '0002';
	'SELECT COMPANY FROM OPT_RES_COM WHERE FUNCT = 'RS' AND TYPE = 'WO';

'--Use this Method when Generated from a Sales Order
	'SELECT F_BOOLEAN FROM OP_HEADER WHERE ID = 401172 AND SEQUENCE = '0003';

'--Use Prefix Associated with User ID
	'SELECT F_BOOLEAN FROM OP_HEADER WHERE ID = 401172 AND SEQUENCE = '0004';

Program.Sub.Comments.End